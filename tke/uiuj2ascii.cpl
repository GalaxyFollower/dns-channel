! This program converst the output tke.bin of tke.cpl
! into several human-readable ASCII files that represents
! the energy box. This is needed because ASCII writing is
! typically done serially.

gamma=0
outinterv=10
USE ../dnsdata
USE ../dnsdirect
USE rtchecks
USE convenience

! Definitions
! ------------------------------------
#define sumint(g) [SUM yintf(g(*,i)) FOR i=1 TO 3]
ARRAY(1..6) OF STRING terms, intterms
terms(1)="uu.dat"; terms(2)="vv.dat"; terms(3)="ww.dat"; terms(4)="uv.dat"; terms(5)="uw.dat"; terms(6)="vw.dat"
intterms(1)="uuintegrals.dat"; intterms(2)="vvintegrals.dat"; intterms(3)="wwintegrals.dat";
intterms(4)="uvintegrals.dat"; intterms(5)="uwintegrals.dat"; intterms(6)="vwintegrals.dat"
MEANTERMS = STRUCTURE(REAL U,W,Uy,Wy,Uyy,Wyy,P)
MKETERMS = STRUCTURE(REAL pump,produv,prodvw,ttrsp,vdiff,dissU,dissW,PHIttrsp,PHIvdiff)
BALANCE = STRUCTURE(ARRAY(1..6) OF REAL var,prod,psdiss,ttrsp,vdiff,pstrain,ptrsp,PHIttrsp,PHIvdiff,PHIptrsp)
POINTER TO STORED STRUCTURE[
        ARRAY(-1..ny+1) OF MEANTERMS meandata
        ARRAY(-1..ny+1) OF MKETERMS mkedata
        ARRAY(-1..ny+1) OF BALANCE uiujdata
] uiujimage

! Write to ASCII file
! ------------------------------------
! reynolds tensor
uiujimage = OPEN("uiuj.bin")
LOOP FOR i=1 TO 6
  FILE outfile=CREATE(terms(i))
  WRITE TO outfile "y","var","prod","psdiss","ttrsp","vdiff","pstrain","ptrsp","PHIttrsp","PHIvdiff","PHIptrsp"
  DO WITH uiujimage.uiujdata(iy):
    WRITE TO outfile y(iy),var(i),prod(i),psdiss(i),ttrsp(i),vdiff(i),pstrain(i),ptrsp(i),PHIttrsp(i),PHIvdiff(i),PHIptrsp(i)
  FOR iy=0 TO ny
  CLOSE(outfile)
REPEAT
! reynolds tensor - integrals
LOOP FOR i=1 TO 6
  FILE outfile=CREATE(intterms(i))
  WRITE TO outfile "var","prod","psdiss","ttrsp","vdiff","pstrain","ptrsp"
  WITH uiujimage.uiujdata: WRITE TO outfile yintf(var(*,i)),yintf(prod(*,i)),yintf(psdiss(*,i)),yintf(ttrsp(*,i)),
                                            yintf(vdiff(*,i)),yintf(pstrain(*,i)),yintf(ptrsp(*,i))
  CLOSE(outfile)
REPEAT
! tke
FILE outfile=CREATE("tke.dat")
WRITE TO outfile "y","var","prod","psdiss","ttrsp","vdiff","pstrain","ptrsp","PHIttrsp","PHIvdiff","PHIptrsp"
DO WITH uiujimage.uiujdata(iy):
  WRITE TO outfile y(iy),SUM(var(1..3)),SUM(prod(1..3)),SUM(psdiss(1..3)),SUM(ttrsp(1..3)),SUM(vdiff(1..3)),
                   SUM(pstrain(1..3)),SUM(ptrsp(1..3)),SUM(PHIttrsp(1..3)),SUM(PHIvdiff(1..3)),SUM(PHIptrsp(1..3))
FOR iy=0 TO ny
CLOSE(outfile)
! tke - integrals
FILE outfile=CREATE("tkeintegrals.dat")
WRITE TO outfile "var","prod","psdiss","ttrsp","vdiff","pstrain","ptrsp"
WITH uiujimage.uiujdata(*): WRITE TO outfile sumint(var),sumint(prod),sumint(psdiss),sumint(ttrsp),sumint(vdiff),sumint(pstrain),sumint(ptrsp)
CLOSE(outfile)
! mke
FILE outfile=CREATE("mke.dat")
WRITE TO outfile "y","pump","produv","prodvw","ttrsp","vdiff","dissU","dissW","PHIttrsp","PHIvdiff"
DO WITH uiujimage.mkedata(iy): WRITE TO outfile y(iy),pump,produv,prodvw,ttrsp,vdiff,dissU,dissW,PHIttrsp,PHIvdiff FOR iy=0 TO ny
CLOSE(outfile)
! mke - integrals
FILE outfile=CREATE("mkeintegrals.dat")
WRITE TO outfile "pump","produv","prodvw","ttrsp","vdiff","dissU","dissW"
WITH uiujimage.mkedata: WRITE TO outfile yintf(pump),yintf(produv),yintf(prodvw),yintf(ttrsp),yintf(vdiff),yintf(dissU),yintf(dissW)
CLOSE(outfile)
! var
FILE outfile=CREATE("var.dat")
WRITE TO outfile "y","uu","vv","ww","uv","uw","vw"
DO WITH uiujimage.uiujdata(iy): WRITE TO outfile y(iy),var(1),var(2),var(3),var(4),var(5),var(6) FOR iy=0 TO ny
CLOSE(outfile)
! mean
FILE outfile=CREATE("mean.dat")
WRITE TO outfile "y","U","W","Uy","Wy","Uyy","Wyy"
DO WITH uiujimage.meandata(iy): WRITE TO outfile y(iy),U,W,Uy,Wy,Uyy,Wyy FOR iy=0 TO ny
CLOSE(outfile)
CLOSE(uiujimage)
